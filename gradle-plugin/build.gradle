plugins {
    id 'com.gradle.plugin-publish' version '0.9.10'
    id 'org.jetbrains.kotlin.jvm' version '1.3.11'
    // For publishing to local maven repo during development
    id 'maven-publish'
}

apply plugin: 'maven'

group = 'ws.osiris'
version = '0.11.2-SNAPSHOT'

ext {
    kotlinVersion = '1.3.11'
    osirisVersion = '0.11.2-SNAPSHOT'
}

repositories {
    mavenLocal()
    jcenter()
}

dependencies {
    compile gradleApi()
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"
    compile "ws.osiris:osiris-archetype:$osirisVersion"
    compile "ws.osiris:osiris-aws-deploy:$osirisVersion"
}

compileKotlin {
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

pluginBundle {
    website = 'http://www.osiris.ws/'
    vcsUrl = 'https://github.com/cjkent/osiris'
    description = 'Plugins for the Osiris Project'
    tags = ['osiris', 'aws', 'serverless', 'apigateway', 'lambda', 'web']

    plugins {
        projectPlugin {
            id = 'ws.osiris.project'
            displayName = 'Osiris Project Plugin'
            description = 'Generates a project for building an application with Osiris'
        }
        deployPlugin {
            id = 'ws.osiris.deploy'
            displayName = 'Osiris Deploy Plugin'
            description = 'Builds the artifacts for an Osiris application, generates the configuration and deploys to AWS'
        }
    }
}

// Publishes the plugin to the local Maven repo during development
// Use: gradlew publishToMavenLocal
// Consuming projects will need to apply plugins using the old style buildscript syntax:
//
//   buildscript {
//       repositories {
//           jcenter()
//           mavenLocal()
//       }
//       dependencies {
//           classpath 'ws.osiris:osiris-gradle-plugin:0.11.2-SNAPSHOT'
//       }
//   }
//
publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'ws.osiris'
            artifactId = 'osiris-gradle-plugin'
            version = '0.11.2-SNAPSHOT'
            from components.java
        }
    }
}
